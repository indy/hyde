---
layout: book
bookname: The Elements of Computing Systems
title: Chapter 02 - Boolean Arithmetic
bookcover: /img/bookcover-tecs.png
amazonuk: http://www.amazon.co.uk/gp/product/0262640686/ref=oss_product
amazonus: http://www.amazon.com/Elements-Computing-Systems-Building-Principles/dp/0262640686/ref=sr_1_1?ie=UTF8&s=books&qid=1284287043&sr=8-1
---
<h2>HalfAdder</h2>
<pre><code>CHIP HalfAdder {

    IN  a, b;
    OUT sum,   // LSB of a + b
        carry; // MSB of a + b

    PARTS:
    And(a=a,b=b,out=carry);
    Xor(a=a,b=b,out=sum);
}</code></pre>
<h2>FullAdder</h2>
<pre><code>CHIP FullAdder {

    IN  a, b, c;
    OUT sum,     // LSB of a + b + c
        carry;   // MSB of a + b + c

    PARTS:
    HalfAdder(a=b,b=c,sum=sumA,carry=carryA);
    Or(a=b,b=c,out=orOut);
    Not(in=sumA,out=notSumA);

    Mux(a=sumA,b=notSumA,sel=a,out=sum);
    Mux(a=carryA,b=orOut,sel=a,out=carry);
}</code></pre>
<h2>Add16</h2>
<pre><code>CHIP Add16 {

    IN  a[16], b[16];
    OUT out[16];

    PARTS:
    HalfAdder(a=a[0],b=b[0],sum=out[0],carry=c0);
    FullAdder(a=c0,b=a[1],c=b[1],sum=out[1],carry=c1);
    FullAdder(a=c1,b=a[2],c=b[2],sum=out[2],carry=c2);
    FullAdder(a=c2,b=a[3],c=b[3],sum=out[3],carry=c3);
    FullAdder(a=c3,b=a[4],c=b[4],sum=out[4],carry=c4);
    FullAdder(a=c4,b=a[5],c=b[5],sum=out[5],carry=c5);
    FullAdder(a=c5,b=a[6],c=b[6],sum=out[6],carry=c6);
    FullAdder(a=c6,b=a[7],c=b[7],sum=out[7],carry=c7);
    FullAdder(a=c7,b=a[8],c=b[8],sum=out[8],carry=c8);
    FullAdder(a=c8,b=a[9],c=b[9],sum=out[9],carry=c9);
    FullAdder(a=c9,b=a[10],c=b[10],sum=out[10],carry=c10);
    FullAdder(a=c10,b=a[11],c=b[11],sum=out[11],carry=c11);
    FullAdder(a=c11,b=a[12],c=b[12],sum=out[12],carry=c12);
    FullAdder(a=c12,b=a[13],c=b[13],sum=out[13],carry=c13);
    FullAdder(a=c13,b=a[14],c=b[14],sum=out[14],carry=c14);
    FullAdder(a=c14,b=a[15],c=b[15],sum=out[15],carry=c15);
}</code></pre>
<h2>Inc16</h2>
<pre><code>CHIP Inc16 {

    IN  in[16];
    OUT out[16];

    PARTS:
    HalfAdder(a=in[0],b=true,sum=out[0],carry=c0);
    FullAdder(a=c0,b=in[1],c=false,sum=out[1],carry=c1);
    FullAdder(a=c1,b=in[2],c=false,sum=out[2],carry=c2);
    FullAdder(a=c2,b=in[3],c=false,sum=out[3],carry=c3);
    FullAdder(a=c3,b=in[4],c=false,sum=out[4],carry=c4);
    FullAdder(a=c4,b=in[5],c=false,sum=out[5],carry=c5);
    FullAdder(a=c5,b=in[6],c=false,sum=out[6],carry=c6);
    FullAdder(a=c6,b=in[7],c=false,sum=out[7],carry=c7);
    FullAdder(a=c7,b=in[8],c=false,sum=out[8],carry=c8);
    FullAdder(a=c8,b=in[9],c=false,sum=out[9],carry=c9);
    FullAdder(a=c9,b=in[10],c=false,sum=out[10],carry=c10);
    FullAdder(a=c10,b=in[11],c=false,sum=out[11],carry=c11);
    FullAdder(a=c11,b=in[12],c=false,sum=out[12],carry=c12);
    FullAdder(a=c12,b=in[13],c=false,sum=out[13],carry=c13);
    FullAdder(a=c13,b=in[14],c=false,sum=out[14],carry=c14);
    FullAdder(a=c14,b=in[15],c=false,sum=out[15],carry=c15);
}</code></pre>
<h2>ALU</h2>
<pre><code>CHIP ALU {

    IN  // 16-bit inputs:
        x[16], y[16],
        // Control bits:
        zx, // Zero the x input
        nx, // Negate the x input
        zy, // Zero the y input
        ny, // Negate the y input
        f,  // Function code: 1 for add, 0 for and
        no; // Negate the out output

    OUT // 16-bit output
        out[16],

        // ALU output flags
        zr, // 1 if out=0, 0 otherwise
        ng; // 1 if out<0, 0 otherwise

    PARTS:
    // zero'd x input
    Mux16(a=x,b[0..15]=false,sel=zx,out=xx);

    // negated'd x input
    Not16(in=xx,out=notxx);
    Mux16(a=xx,b=notxx,sel=nx,out=xxx);

    // zero'd y input
    Mux16(a=y,b[0..15]=false,sel=zy,out=yy);

    // negated'd y input
    Not16(in=yy,out=notyy);
    Mux16(a=yy,b=notyy,sel=ny,out=yyy);

    // f
    Add16(a=xxx,b=yyy,out=addxy);
    And16(a=xxx,b=yyy,out=andxy);
    Mux16(a=andxy,b=addxy,sel=f,out=fxy);

    // negated'd out output
    Not16(in=fxy,out=notfxy);
    Mux16(a=fxy,b=notfxy,sel=no,out=out2);

    // zr
    Mux16(a=out2,b=out2,sel=true,out[0..7]=ha,out[8..15]=hb);
    Or8Way(in=ha,out=ora);
    Or8Way(in=hb,out=orb);
    Or(a=ora,b=orb,out=orc);
    Not(in=orc,out=zr);

    //ng and out
    And16(a=out2,b[0..15]=true,out[15]=ng, out=out);
}</code></pre>
